alias functionNum R1;
alias currentPID R2;

//Get PCB Entry
if(functionNum == 1) then
	alias i R3;
	i=0;
	while(i<16) do
		if([PROCESS_TABLE+16*i+4] == TERMINATED) then
			[PROCESS_TABLE+16*i+1] = i;
			[PROCESS_TABLE+16*i+4] = ALLOCATED;
			[PROCESS_TABLE+16*i+14] = PAGE_TABLE_BASE+20*i;
			[PROCESS_TABLE+16*i+15] = 10;
			R0 = i;
			return;
		endif;
		i = i+1;
	endwhile;
	R0 = -1;
endif;


//Free User Area Page
if(functionNum == 2) then
	alias i R10;
	i = 0;
	multipush(R0,R1,R2,R10);
		while(i<8) do
			if([[PROCESS_TABLE +currentPID*16+11]*512+2*i+496]==1) then
				// Release Semaphore
				R1 = 7;
				R2 = currentPID;
				R3 = [[PROCESS_TABLE +currentPID*16+11]+2*i+496+1];
				call RESOURCE_MANAGER;
			endif;
			i = i+1;
		endwhile;
	multipop(R0,R1,R2,R10);
	alias userPage R3;
	userPage = [PROCESS_TABLE+currentPID*16+11];
	multipush(R0,R1,R2,R3);
	R1 = 2;
	R2 = userPage;
	//Calling Release page in Memory Manager Module
	call MEMORY_MANAGER;
	multipop(R0,R1,R2,R3);
endif;

//Exit
if(functionNum == 3) then
	multipush(R0,R1,R2,R3);
	R1 = 4;
	R2 = currentPID;
	//Calling Free Page Table in Process Manager Module
	call PROCESS_MANAGER;
	multipop(R0,R1,R2,R3);
	multipush(R0,R1,R2,R3);
	R1 = 2;
	R2 = currentPID;
	//Calling Free User Area Page in Process Manager Module
	call PROCESS_MANAGER;
	multipop(R0,R1,R2,R3);
	alias i R4;
	i=0;
	while(i<16) do
	//When exit call not from exec
	if([PROCESS_TABLE+currentPID*16+9]!=9) then
	    if(([PROCESS_TABLE+i*16+4]==WAIT_PROCESS)&&([PROCESS_TABLE+i*16+5]==currentPID)) then
		[PROCESS_TABLE+i*16+4]=READY;
	    endif;
	    if([PROCESS_TABLE+i*16+2]==currentPID) then
		[PROCESS_TABLE+i*16+2]=-1;
	    endif;
	endif;
	i=i+1;
	endwhile;
	[PROCESS_TABLE+[SYSTEM_STATUS_TABLE+1]*16+4] = TERMINATED;
endif;

//Free Page Table
if(functionNum == 4) then
	PTBR = [PROCESS_TABLE+[SYSTEM_STATUS_TABLE+1]*16+14];
	PTLR = [PROCESS_TABLE+[SYSTEM_STATUS_TABLE+1]*16+15];
	[PTBR+0] = -1;
	[PTBR+1] = "0000";
	[PTBR+2] = -1;
	[PTBR+3] = "0000";
	alias i R3;
	i = 2;
	while(i<PTLR) do
		if([PTBR+i*2] != -1) then
			multipush(R0,R1,R2,R3);
			R1 = 2;
			R2 = [PTBR+i*2];
			//Calling Release page in Memory Manager Module
			call MEMORY_MANAGER;
			multipop(R0,R1,R2,R3);
			[PTBR+i*2] = -1;
			[PTBR+i*2+1] = "0000";
		endif;
		i = i+1;
	endwhile;
	i=2;
	while(i<4) do
		if([DISK_MAP_TABLE+10*currentPID+i] != -1) then
			multipush(R0,R1,R2,R3,R4);
			//Release Block
			R1 = 4;
			R2 = [DISK_MAP_TABLE+10*currentPID+i];
			R3 = currentPID;
			call MEMORY_MANAGER;
			multipop(R0,R1,R2,R3,R4);
		endif;
		i = i+1;
	endwhile;
	i=8;
	while(i<10) do
		if([DISK_MAP_TABLE+10*currentPID+i] != -1) then
			multipush(R0,R1,R2,R3,R4);
			//Release Block
			R1 = 4;
			R2 = [DISK_MAP_TABLE+10*currentPID+i];
			R3 = currentPID;
			call MEMORY_MANAGER;
			multipop(R0,R1,R2,R3,R4);
		endif;
		i = i+1;
	endwhile;
	i=0;
	while(i<10) do
		[DISK_MAP_TABLE+10*currentPID+i] = -1;
		i = i+1;
	endwhile;
endif;

return;
